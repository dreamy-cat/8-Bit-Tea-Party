;8-Бит Чаепитие! 2021.
;Лицензия: Creative Commons.
;Платформа: 8086, VGA(64Kb), BIOS.
;Инструменты: NASM(2.15.05).
;Модули: RND_S.
;Авторы: Александр Серов(funny.dreamy.cat@gmail.com).
;Краткая история изменений:
;09.10.21, А.С.: Демонстрация использования процедуры на видеорежиме 13h.

RANDOM_INIT     EQU 01AF0h              ;Первоначальные данные для теста.

CPU 8086                                ;Директива ассемблера, изменить.

        org 100h                        ;Минимальный COM-файл.
        pushf                           ;Сохраним хотя бы флаги.
        mov ax,0013h                    ;Устанавливаем стандартный режим 13h,
        int 10h                         ;320*200*256 цветов, через БИОС.
        xor cx,cx                       ;Счетчик последовательности 64К.
        mov ax,0A000h                   ;Инициализация сегментного адреса
        mov es,ax                       ;видеобуфера в регистр ES.
        mov ax,RANDOM_INIT              ;Начальное значение, любое, кроме 0.
        call near RandomSimple          ;Инициализация последовательности.
R1:     xor ax,ax                       ;Обнуляем акк. для получения
        call near RandomSimple          ;псевдослучайного числа.
        cmp ax,64000                    ;Немного подстрахуемся на переполнение
        jnc short R1                    ;буфера экрана.
        mov di,ax                       ;Используем простое линейное смещение.
        mov byte es:[di],0Fh            ;Ставим стандартную точку цветом 15.
        loop R1                         ;Замыкаем основной цикл.
        mov ah,00h                      ;Ожидаем нажатия любой клавиши
        int 16h
        popf                            ;Восстанавливаем флаги и выходим.
        ret

%include "rnd_s.asm"                    ;Пока вызов на уровне исходников.
